// Chapter 1
// Highest label: 0xF
// Temporary flags used:
//    0x5 set by battle quote when Simon attacks any of the mercs. If Simon attacks Turner, it's set by a different event called by a battlequote.
//    0x7 set when visiting the village
//    0x8 set after AFEV that runs after 0x5 is set. Turns enemy AI hostile to Simon as well.
//    0x9 set by battle quote when Turner attacks Simon or Simon initiates battle against Turner, before having fought any of the other mercs.
//    0xA set by talk conversation between Demi & Milo/Leona
//    0xB set by turnbased event. Changes AI of some units to be more aggresive.
{
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno Tutorial
POIN Traps1 Traps2
POIN PrepUnits PrepUnits  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

Dunno:
END_MAIN

Tutorial:
END_MAIN

PrepUnits:
UNIT

PlayerUnits:
UNIT Vivian Supplier Vivian Level(0x2, Ally, 0x0) [10,3] 0 0 1 REDA1 [0x0] NoAI
UNIT Simon  Shaman   Vivian Level(0x8, Ally, 0x0) [10,3] 0 0 3 REDA2 [0x0] AttackInRangeAI
UNIT

REDA1:
REDA [10,5] 0 0x80 0 0

REDA2:
REDA [10,5] 0 0x80 0 0
REDA [8,5] 0 0x80 0 0
REDA [8,4] 0 0x80 0 0

Unit3_0:
UNIT Leona Mercenary_F Vivian Level(0x3, Ally, 0x0) [10,3] 0 0 1 REDA3 [IronSword, Vulnerary] NoAI
UNIT

Unit3_1:
UNIT Leona Fighter_F Vivian Level(0x3, Ally, 0x0) [10,3] 0 0 1 REDA3 [IronAxe, Vulnerary] NoAI
UNIT

Unit3_2:
UNIT Leona Soldier_F Vivian Level(0x3, Ally, 0x0) [10,3] 0 0 1 REDA3 [IronLance, Vulnerary] NoAI
UNIT

Unit3_3:
UNIT Leona Archer_F Vivian Level(0x3, Ally, 0x0) [10,3] 0 0 1 REDA3 [IronBow, Vulnerary] NoAI
UNIT

Unit3_4:
UNIT Milo Shaman Vivian Level(0x3, Ally, 0x0) [10,3] 0 0 1 REDA3 [Flux, Vulnerary] NoAI
UNIT

Unit3_5:
UNIT Milo Mage Vivian Level(0x3, Ally, 0x0) [10,3] 0 0 1 REDA3 [Fire, Vulnerary] NoAI
UNIT

Unit3_6:
UNIT Milo Monk Vivian Level(0x3, Ally, 0x0) [10,3] 0 0 1 REDA3 [Lightning, Vulnerary] NoAI
UNIT

Unit3_7:
UNIT Milo Priest Vivian Level(0x3, Ally, 0x0) [10,3] 0 0 1 REDA3 [Heal, Vulnerary] NoAI
UNIT

REDA3:
REDA [10,4] 0 0x80 0 0

Unit4:
UNIT Demi Soldier_F 0x0 Level(0x4, NPC, 0x0) [9, 6] 0 0 0 0 [IronLance, Vulnerary] AttackInRangeAI
UNIT

EnemyUnits:
UNIT 0x48 Mercenary Turner Level(0x5, Enemy, 0x1) [3,0] 0 0 0 0 [IronSword] [DontAttackSimon, 3, 0, 0]
UNIT 0x48 Mercenary_F Turner Level(0x3, Enemy, 0x1) [3,5] 0 0 0 0 [IronSword] [DontAttackSimon, 3, 0, 0]
UNIT 0x49 Mercenary Turner Level(0x5, Enemy, 0x0) [4,9] 0 0 0 0 [IronSword] IgnoreSimonAI
UNIT 0x4A Mercenary_F Turner Level(0x4, Enemy, 0x0) [12,6] 0 0 0 0 [IronSword] IgnoreSimonAI
UNIT Turner Mercenary Turner Level(0x5, Enemy, 0x0) [0,8] 0 0 0 0 [SteelSword] [DontAttackSimon, 3, 0, 0]
UNIT

CutsceneUnits:
UNIT 0x4B Soldier Vivian Level(0x5, NPC, 0x0) [7,6] 0 0 0 0 [IronLance] NoAI
UNIT 0x4C Soldier Vivian Level(0x5, NPC, 0x0) [9,7] 0 0 0 0 [IronLance] NoAI
UNIT Aiden Myrmidon_F Turner Level(0x5, Enemy, 0x0) [1,8] 0 0 0 0 [SteelSword] NoAI
UNIT


BeginningScene:
MUSC 0x25
LOAD1 1 CutsceneUnits
ENUN
LOAD1 1 Unit4
ENUN
SVAL 0x1 0x7
SET_HP 0x4B
SVAL 0x1 0x6
SET_HP 0x4C
LOAD1 1 EnemyUnits
ENUN
FADU 16

// Turner dia- & monologue
CUMO Turner
STAL 60
CURE
Text(Plain_2,Chapter1_1)
MOVE 0x80 Aiden [1,9]
ENUN
MOVE 0x80 Aiden [0,9]
ENUN
DISA Aiden
CUMO Turner
STAL 60
CURE
Text(Chapter1_2)


// Merc1 kills soldier1
MOVE 0x80 0x49 [7,7]
ENUN
StartBattle
NormalDamage(0x0, 0xB)
EndAttack
FIGHT_MAP 0x49 0x4B 0x0 0x0
ENUN
AWAIT_KILL 0x4B
ENUN

// Merc2 kills soldier2
MOVE 0x80 0x4A [10,7]
ENUN
StartBattle
NormalDamage(0x0, 0xA)
EndAttack
FIGHT_MAP 0x4A 0x4C 0x0 0x0
ENUN
AWAIT_KILL 0x4C
ENUN

// Demi monologue
CUMO Demi
STAL 60
CURE
Text(Chapter1_9)

// Heroes enter the scene
LOAD2 0 PlayerUnits
ENUN
CUMO Vivian
STAL 60
CURE
Text(Plain_2,Chapter1_3)

// Prepare unit 3
ASMC Ch1Unit3+0x1
BEQ 0x0 0x0 0xC
  SVAL 0x1 0x1
  BEQ 0x1 0x1 0xC
    SVAL 0x1 0x2
    BEQ 0x2 0x1 0xC
      SVAL 0x1 0x3
      BEQ 0x3 0x1 0xC
        SVAL 0x1 0x4
        BEQ 0x4 0x1 0xC
          SVAL 0x1 0x5
          BEQ 0x5 0x1 0xC
            SVAL 0x1 0x6
            BEQ 0x6 0x1 0xC
              GOTO 0x7

LABEL 0x0
LOAD1 1 Unit3_0
GOTO  0x8
  LABEL 0x1
  LOAD1 1 Unit3_1
  GOTO  0x8
    LABEL 0x2
    LOAD1 1 Unit3_2
    GOTO  0x8
      LABEL 0x3
      LOAD1 1 Unit3_3
      GOTO  0x8
        LABEL 0x4
        LOAD1 1 Unit3_4
        GOTO  0x8
          LABEL 0x5
          LOAD1 1 Unit3_5
          GOTO  0x8
            LABEL 0x6
            LOAD1 1 Unit3_6
            GOTO  0x8
              LABEL 0x7
              LOAD1 1 Unit3_7
LABEL 0x8
ENUN
CUMO [10,4]
STAL 60
CURE
Text(Plain_2,Chapter1_4)

// Turner's final comment before battle
CUMO Turner
STAL 60
CURE
Text(Chapter1_5)

// Message informing player about new guide entries.
EVBIT_MODIFY 0x4
CHECK_EVBIT 0x8
BEQ 0xD 0xC 0x0
  // Fade in if we aren't faded in yet (when player skipped to this point)
  FADU 16
LABEL 0xD
_0x1A25
SCOORD 0xB [12,4]
TEXTSHOW Chapter1_8
TEXTEND
REMA
EVBIT_MODIFY 0x0

NoFade
ENDA

LocationBasedEvents:
Village(0x7,VillageEvent,1,1)
// TODO remove this as soon as you've used it in some other chapter's events.
//LOCA 0x7 Ch1PuzzleEvent [7,3] 0x1A
END_MAIN

EndingScene:
ASMC ChapterEndExtra+0x1

// Outro text.
MUSC 0x31
SetBackground(Plain_2)
TEXTSHOW Chapter1_7
TEXTEND
REMA

// Demi & Leona/Milo confirm "Scenario Beta".
CHECK_ALLEGIANCE Demi
BEQ 0xF 0x0 0xC
  SetBackground(Plain_2)
  TEXTSHOW Chapter1_11
  TEXTEND
  REMA
  FADI 16
  CUSA Demi
LABEL 0xF

MNCH 0x2
ENDA

TurnBasedEvents:
TurnEventPlayer(0xB,ChangeAIEvent2,2)
END_MAIN

CharacterBasedEvents:
CharacterEvent(0xA,DemiLeonaMiloTalk,Leona,Demi)
CharacterEvent(0xA,DemiLeonaMiloTalk,Milo,Demi)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
DefeatBoss(EndingScene)
AFEV 0x8 ChangeAIEvent 0x5
END_MAIN

Traps1:
ENDTRAP

Traps2:
ENDTRAP

ALIGN 4
VillageEvent:
VillageEventItem(Ch1VillageText, HouseBG, SlimLance)

// Executed if Simon attacks an enemy unit.
ChangeAIEvent:
CUMO Turner
STAL 60
CURE
Text(Chapter1_6)
SVAL 0x1 0x0                            // Default AI
CHAI Turner
CHAI 0x48
CHAI 0x49
CHAI 0x4A
NoFade
ENDA

// Executed when Milo or Leona initiates a conversation with Demi
DemiLeonaMiloTalk:
Text(Chapter1_10)
CUSA Demi
NoFade
ENDA

// Executed on turn 2, if Simon hasn't turned hostile yet.
// Makes remaining passive enemies also chase,
// although they'll still ignore Simon.
ChangeAIEvent2:
CHECK_EVENTID 0x5
BNE 0xE 0x0 0xC
  SVAL 0x1 DontAttackSimon|DontPursueSimon<<8
  CHAI Turner
  CHAI 0x48
  CHAI 0x49
  CHAI 0x4A
LABEL 0xE
NoFade
ENDA

// TODO remove this as soon as you've used it in some other chapter's events.
/*
Ch1PuzzleEvent:
MUSC 0x9
StartPuzzle(0x1, 0x2, 0x0, 0x0, 0x7)
MUSC 0xA
NoFade
ENDA
*/
}

IsSimonHostileEvent:
CHECK_EVENTID 0x5
BNE 0x9 0x0 0xC
  TEXTSHOW Ch1BattleQuote3
  GOTO 0xA
LABEL 0x9
  TEXTSHOW Ch1BattleQuote4
LABEL 0xA
TEXTEND
REMA
ENUT 0x5
ENDA