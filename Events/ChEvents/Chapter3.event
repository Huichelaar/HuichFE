/*  Chapter 3
    Highest label: 10
    Temporary flags used:
      0x5 House visited.
      0x7 Raided fortress 1
      0x8 Raided fortress 2
      0x9 Raided fortress 3
      0xA Raided fortress 4
      0xB Vivian & Linda talk. Also allows seize.
      0xC Vivian & Alvin BQ.
      0xD Alvin generic BQ.
      0xE := Flag 0xB | 0xC | 0xD. Vivian & Alvin talk.
      0xF Alvin & Linda BQ.
*/
{
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno Tutorial
POIN Traps1 Traps2
POIN PrepUnits PrepUnits  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

Dunno:
END_MAIN

Tutorial:
END_MAIN

PrepUnits:
UNIT

PlayerUnits1:
UNIT Demi Soldier_F Vivian Level(1, Ally, 0) [1,1] 0 0 0 0 [] NoAI
UNIT Theresa Archer_F Vivian Level(1, Ally, 0) [0,2] 0 0 0 0 [] NoAI
UNIT Herbert Cavalier Vivian Level(1, Ally, 0) [0,0] 0 0 0 0 [] NoAI
UNIT

PlayerUnits2L:
UNIT Vivian Supplier Vivian Level(1, Ally, 0) [5,0] 0 0 2 REDA1 [] NoAI
UNIT Leona Mercenary_F Vivian Level(1, Ally, 0) [6,0] 0 0 1 REDA2 [] NoAI
UNIT
PlayerUnits2M:
UNIT Vivian Supplier Vivian Level(1, Ally, 0) [5,0] 0 0 2 REDA1 [] NoAI
UNIT Milo Monk Vivian Level(1, Ally, 0) [6,0] 0 0 1 REDA2 [] NoAI
UNIT
  REDA1:
  REDA [3,0] 0 0x80 0 0
  REDA [3,1] 0 0x80 0 0
  REDA2:
  REDA [4,0] 0 0x80 0 0
  
SimonUnit:
UNIT Simon Shaman 0 Level(1, NPC, 0) [12,19] 0 0 0 0 [] NoAI
UNIT

AlvinUnit:
UNIT Alvin Knight Alvin Level(12, Enemy, 0) [16,19] 0 0 0 0 [IronLance, Horseslayer, Concoction] [0x03,0x03,0x0,0x20]
UNIT

EnemyUnits1:
UNIT 0x58 Fighter_F Alvin Level(7, Enemy, 1) [13,20] 0 0 0 0 [IronLance] GroupEightAI
UNIT 0x59 Mercenary_F Alvin Level(7, Enemy, 1) [18,17] 0 0 1 REDA3 [IronLance] GroupNineAI
UNIT 0x59 Monk Alvin Level(8, Enemy, 1) [18,17] 0 0 1 REDA4 [IronLance] GroupNineAI
UNIT 0x5A Priest Alvin Level(6, Enemy, 1) [18,17] 0 0 2 REDA5 [Mend] AttackInRangeAI
UNIT Linda Cavalier Alvin Level(7, Enemy, 1) [16,19] 0 0 4 REDA6 [] AttackInRangeAI
UNIT
  REDA3:
  REDA [15,17] 0 0x80 0 0
  REDA4:REDA5:
  REDA [17,17] 0 0x80 0 0
  REDA [17,15] 0 0x80 0 0
  REDA6:
  REDA [16,20] 0 0x80 0 0
  REDA [9,20] 0 0x80 0 0
  REDA [9,16] 0 0x80 0 0
  REDA [8,16] 0 0x80 0 0

EnemyUnits2:
UNIT 0x55 Cavalier Alvin Level(8, Enemy, 1) [8,16] 0 0 0 0 [IronLance] GroupFiveAI
UNIT 0x58 PegasusKnight Alvin Level(6, Enemy, 1) [8,16] 0 0 2 REDA7 [IronLance] GroupEightAI
UNIT 0x58 Soldier_F Alvin Level(7, Enemy, 1) [8,16] 0 0 2 REDA8 [IronLance] GroupEightAI
UNIT 0x57 PegasusKnight Alvin Level(8, Enemy, 1) [8,16] 0 0 2 REDA9 [IronLance] GroupSevenAI
UNIT Linda Cavalier Alvin Level(1, Enemy, 0) [8,16] 0 0 1 REDA10 [] NoAI
UNIT
  REDA7:
  REDA [8,17] 0 0x80 0 0
  REDA [11,17] 0 0x80 0 0
  REDA8:
  REDA [9,16] 0 0x80 0 0
  REDA [9,19] 0 0x80 0 0
  REDA9:
  REDA [8,20] 0 0x80 0 0
  REDA [3,20] 0 0x80 0 0
  REDA10:
  REDA [8,9] 0 0x80 0 0

LindaUnit1:
UNIT Linda Shaman_F Alvin Level(1, Enemy, 0) [16,19] 0 0 1 REDA6 [] NoAI
UNIT

EnemyUnits3:    // Loadbuffer overlaps with debufftable. Hence we split these in two.
UNIT 0x57 PegasusKnight Alvin Level(7, Enemy, 1) [1,18] 0 0 0 0 [IronLance] GroupSevenAI
UNIT 0x56 PegasusKnight Alvin Level(9, Enemy, 1) [3,11] 0 0 0 0 [IronLance, Javelin] GroupSixAI
UNIT 0x56 PegasusKnight Alvin Level(9, Enemy, 1) [0,8] 0 0 0 0 [IronLance, Javelin] GroupSixAI
UNIT 0x51 Cavalier_F Alvin Level(6, Enemy, 1) [3,4] 0 0 0 0 [IronLance] GroupOneAI
UNIT 0x51 Fighter Alvin Level(8, Enemy, 1) [6,0] 0 0 0 0 [IronAxe] GroupOneAI
UNIT 0x52 Fighter_F Alvin Level(6, Enemy, 1) [13,5] 0 0 0 0 [IronAxe, Hatchet] GroupTwoAI
UNIT 0x53 Archer Alvin Level(9, Enemy, 1) [10,8] 0 0 0 0 [IronBow] GroupThreeAI
UNIT 0x54 Myrmidon Alvin Level(8, Enemy, 1) [18,9] 0 0 0 0 [IronSword, SlimSword] GroupFourAI
UNIT 0x54 Monk Alvin Level(9, Enemy, 1) [17,10] 0 0 0 0 [Lightning, Vulnerary] GroupFourAI
UNIT 0x5A Mage Alvin Level(10, Enemy, 1) [16,6] 0 0 0 0 [Thunder] AttackInRangeAI
UNIT
EnemyUnits4:
UNIT 0x52 Soldier Alvin Level(6, Enemy, 1) [13,3] 0 0 0 0 [Javelin] GroupTwoAI
UNIT 0x52 Myrmidon_F Alvin Level(9, Enemy, 1) [11,1] 0 0 0 0 [Zanbato] GroupTwoAI
UNIT 0x53 Myrmidon Alvin Level(8, Enemy, 1) [9,6] 0 0 0 0 [IronSword, SteelSword] GroupThreeAI
UNIT 0x55 Mercenary Alvin Level(7, Enemy, 1) [10,13] 0 0 0 0 [IronBlade] GroupFiveAI
UNIT 0x55 Cavalier Alvin Level(10, Enemy, 1) [11,13] 0 0 0 0 [IronLance, IronSword] GroupFiveAI
UNIT 0x5A Archer_F Alvin Level(8, Enemy, 1) [16,19] 0 0 0 0 [ShortBow] AttackInRangeAI
UNIT 0x5A Soldier_F Alvin Level(11, Enemy, 1) [0,13] 0 0 0 0 [IronLance, Horseslayer] AttackInRangeAI
UNIT 0x5A Mage Alvin Level(7, Enemy, 1) [4,14] 0 0 0 0 [Fire] AttackInRangeAI
UNIT 0x5A Priest Alvin Level(11, Enemy, 1) [14,11] 0 0 0 0 [Heal, Vulnerary] AttackInRangeAI
UNIT 0x51 Archer Alvin Level(10, Enemy, 1) [8,4] 0 0 0 0 [IronBow] GroupOneAI
UNIT

LindaUnit2:
UNIT Linda Shaman_F Alvin Level(9, Enemy, 0) [8,9] 0 0 2 REDA11 [Flux, Luna, Vulnerary] NeverMoveAI
UNIT
  REDA11:
  REDA [6,9] 0 0x80 0 0
  REDA [6,8] 0 0x80 0 0

LindaReinfUnits:
UNIT Carmen Cleric Alvin Level(11, Enemy, 0) [6,8] 0 0 2 REDA12 [Heal, Concoction] NoAI
UNIT Vincent WyvernKnight Alvin Level(10, Enemy, 0) [6,8] 0 0 2 REDA13 [IronAxe, Hatchet, Vulnerary] NoAI
UNIT
  REDA12:
  REDA [6,9] 0 0x80 0 0
  REDA [5,9] 0 0x80 0 0
  REDA13:
  REDA [7,8] 0 0x80 0 0
  REDA [7,9] 0 0x80 0 0

BadEndCutsceneUnits:
UNIT Vivian Supplier Vivian Level(5, Ally, 0) [16,20] 0 0 0 0 [] NoAI
UNIT Alvin Knight Alvin Level(12, Enemy, 0) [16,19] 0 0 0 0 [IronLance] NoAI
UNIT

FortReinfUnits:
UNIT 0x5A Cavalier Alvin Level(9, Enemy, 1) [8,16] 0 0 0 0 [IronLance] NoAI
UNIT
UNIT 0x5B Cavalier_F Alvin Level(11, Enemy, 1) [8,16] 0 0 0 0 [IronLance, IronSword] NoAI
UNIT
UNIT 0x5A PegasusKnight Alvin Level(10, Enemy, 1) [0,20] 0 0 0 0 [SlimLance] NoAI
UNIT
UNIT 0x5B PegasusKnight Alvin Level(12, Enemy, 1) [0,20] 0 0 0 0 [IronLance] NoAI
UNIT
UNIT 0x5A Soldier Alvin Level(7, Enemy, 1) [14,6] 0 0 0 0 [IronLance] NoAI
UNIT
UNIT 0x5B Soldier_F Alvin Level(9, Enemy, 1) [14,6] 0 0 0 0 [Javelin] NoAI
UNIT
UNIT 0x5A Fighter_F Alvin Level(8, Enemy, 1) [8,9] 0 0 0 0 [IronAxe] NoAI
UNIT
UNIT 0x5B Fighter Alvin Level(10, Enemy, 1) [8,9] 0 0 0 0 [IronAxe, HandAxe] NoAI
UNIT


BeginningScene:
LOAD2 0 PlayerUnits1
ENUN
LOAD1 1 SimonUnit
ENUN
REMOVEPORTRAITS
BACG 0x35
FADU 128
FAWI 4
BACG 0x14
_0x1328 0x2C                // Fades in song apparently. Taken from FE8 Ch14.
FAWU 4
BROWNBOXTEXT 0x210 [8,8]
ShowText(Ch3_0)
TEXTSHOW2 Ch3_1             // Allows manipulating previously loaded portraits.
TEXTEND
_0x1328 0x7FFF
FAWI 4
BACG 0x12
REMA
FAWU 4
ShowText(Ch3_2)
REMA
FADI 16
CLEAN
FADU 16

CHECK_EVENTID MiloFlag
BNE 0 0x0 0xC
  LOAD2 0 PlayerUnits2L
  GOTO 1
LABEL 0
  LOAD2 0 PlayerUnits2M
LABEL 1
ENUN
CUMO Demi
STAL 60
CURE
FADI 16
Text(Plain_3,Ch3_3)

CAMERA [18,20]
MOVE 0x80 Simon [16, 19]
ENUN
DISA Simon
CUMO [16, 19]
STAL 60
CURE
FADI 16
Text(Plain_3,Ch3_4)

LOAD1 1 AlvinUnit
ENUN
CUMO Alvin
STAL 60
CURE
Text(Ch3_5)
LOAD1 1 EnemyUnits1
ENUN
DISA Linda
STAL 20
LOAD1 1 EnemyUnits2
ENUN
DISA Linda
LOAD1 1 LindaUnit1
ENUN
CUMO Linda
STAL 60
CURE
FADI 16
LOAD1 1 EnemyUnits3
ENUN
LOAD1 1 EnemyUnits4
ENUN
Text(Plain_3,Ch3_6)

EVBIT_T 0x9
MOVE 0x80 Linda [8, 9]
ENUN
EVBIT_F 0x9
DISA Linda
CUMO [8, 9]
STAL 60
CURE
FADI 16
Text(Plain_3,Ch3_7)

LOAD1 1 LindaUnit2
ENUN
CAMERA [0,0]
CUMO Vivian
STAL 60
CURE
Text(Plain_3,Ch3_8)

CHECK_EVENTID MiloFlag
BNE 2 0x0 0xC
  RemoveRetreatedUnit(Leona)
  GOTO 3
LABEL 2
  RemoveRetreatedUnit(Milo)
LABEL 3
RemoveRetreatedUnit(Demi)
NoFade
ENDA


LocationBasedEvents:
Armory(Ch3Armory,18,1)
Vendor(Ch3Vendor,0,15)
Raid(0x7, Raid1, 8, 16)
Raid(0x8, Raid2, 0, 20)
Raid(0x9, Raid3, 14, 6)
Raid(0xA, Raid4, 8, 9)
Village(0x5,VillageEvent,15,0)
Seize(16, 19)
END_MAIN
  Raid1:
  SVAL 0x3 Hatchet
  GIVEITEM (-1)
  TileChange(14, 6)
  ENDA
  Raid2:
  SVAL 0x3 Hammer
  GIVEITEM (-1)
  TileChange(8, 9)
  ENDA
  Raid3:
  SVAL 0x3 SecretBook
  GIVEITEM (-1)
  TileChange(8, 16)
  ENDA
  Raid4:
  SVAL 0x3 Mend
  GIVEITEM (-1)
  TileChange(0, 20)
  ENDA
  VillageEvent:
  VillageEventMoney(Ch3_13, Salehs_House, 5000)

EndingScene:
ASMC ChapterEndExtra+0x1

CHECK_EVENTID 0xA
BNE 4 0x0 0xC
  // Linda joined already.
  GOTO 5
LABEL 4
  // Linda didn't join yet.
LABEL 5

MNCH 0x4
ENDA


TurnBasedEvents:
TurnEventPlayer(0,FortReinforcements,2,254)
TurnEventEnemy(0,CHAIgroup2,3)                      // Aggro enemies.
TurnEventEnemy(0,CHAIgroup3,2)
TurnEventEnemy(0,CHAIgroup4,5)
TurnEventEnemy(0,CHAIgroup5,7)
TurnEventEnemy(0,CHAIgroup6,6)
TurnEventEnemy(0,CHAIgroup7,10)
TurnEventEnemy(0,CHAIgroup8,12)
TurnEventEnemy(0,CHAIgroup9,14)
END_MAIN
  FortReinforcements:
  CHECK_TURNNUMBER
  SADD 0x4 0x0 0xC
  SVAL 0x2 4
  SMOD 0x5 0xC 0x2
  SVAL 0x3 0x7
  SADD 0x2 0x5 0x3
  CHECK_EVENTID (-1)
  BNE 9 0x0 0xC           // Don't load reinforcements if fort raided.
    SVAL 0x3 FortReinfUnits|0x8000000
    SVAL 0x2 10
    BGT 10 0x2 0x4
      SVAL 0x3 40         // Load zero-exp-granting enemies past turn 10
    LABEL 10
    SVAL 0x2 80
    SMUL 0x2 0x5 0x2      // Determine which fort sends reinforcements.
    SADD 0x2 0x2 0x3      // Forts will alternate.
    LOAD_S2 1
    GOTO 9

  CHAIgroup2:
  SVAL 0x1 0
  CHAI 0x52
  GOTO 9
  CHAIgroup3:
  SVAL 0x1 0
  CHAI 0x53
  GOTO 9
  CHAIgroup4:
  SVAL 0x1 0
  CHAI 0x54
  GOTO 9
  CHAIgroup5:
  SVAL 0x1 0
  CHAI 0x55
  GOTO 9
  CHAIgroup6:
  SVAL 0x1 0
  CHAI 0x56
  GOTO 9
  CHAIgroup7:
  SVAL 0x1 0
  CHAI 0x57
  GOTO 9
  CHAIgroup8:
  SVAL 0x1 0
  CHAI 0x58
  GOTO 9
  CHAIgroup9:
  SVAL 0x1 0
  CHAI 0x59
  LABEL 9
  NoFade
  ENDA


/*
Flag C := B | C | D

if Alvin has fought any unit but Vivian:
  Set flag D, E in generic battle quote.
if Vivian talks to Linda:
  Set flag B, E.
if Vivian attacks Alvin and flag B or D is set, C unset:
  Battle conversation, set flag C, E.
if Vivian attacks Alvin and flag B & C are unset:
  Different battle conversation, sets flag C, E.
if Vivian talks to Alvin, only possible when Flag B, C, D (so E) unset:
  Conversation where Alvin tricks Vivian. Vivian gets killed.
*/
CharacterBasedEvents:
CharacterEvent(0xB,VivianLindaTalk,Vivian,Linda)
CharacterEvent(0xE,VivianAlvinTalk,Vivian,Alvin)
END_MAIN
  VivianLindaTalk:
  CAMERA Vivian
  Text(Plain_3,Ch3_9)
  CUMO Linda
  SOUN 0xC2
  STAL 60
  CURE
  LOAD1 1 LindaReinfUnits
  ENUN
  CUMO Vincent
  STAL 60
  CURE
  Text(Plain_3,Ch3_10)
  SetChapterObjectiveText(0x1A0, 0x1B7) // "Seize gate", "Seize lair gate"
  CUSA Linda
  CUSA Vincent
  CUSA Carmen
  ENUT 0xE
  NoFade
  ENDA
  
  VivianAlvinTalk:
  CUMO Alvin
  STAL 60
  CURE
  FADI 16
  CLEE 0
  CLEN 0
  CLEA 0
  LOAD2 0 BadEndCutsceneUnits
  ENUN
  Text(Plain_3,Ch3_11)
  MOVE 0x80 Vivian [18, 17]
  ENUN
  MOVE 0x80 Alvin [17, 17]
  ENUN
  StartBattle
  CriticalHit(0x0, 0x1E)
  EndAttack
  FIGHT_MAP Alvin Vivian 0x0 0x0
  ENUN
  AWAIT_KILL Vivian
  ENUN
  Text(Ch3_12)
  ENUT 0x65         // GameOver flag.
  ASMC 0x800D391    // Starts event engine & calls GameOver event.
  NoFade
  ENDA


MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN


Traps1:
END_MAIN


Traps2:
END_MAIN
}

AlvinBQEvent:
CHECK_EVENTID 0xB
BNE 6 0x0 0xC
  CHECK_EVENTID 0xD
  BNE 6 0x0 0xC
    TEXTSHOW Ch3_BQ3
    GOTO 7
LABEL 6
  TEXTSHOW Ch3_BQ4
LABEL 7
TEXTEND
REMA
ENUT 0xE
ENDA

Ch3Armory:
SHLI IronSword IronBlade IronLance IronAxe IronBow ShortBow Vulnerary
Ch3Vendor:
SHLI Fire Lightning Flux Heal Vulnerary