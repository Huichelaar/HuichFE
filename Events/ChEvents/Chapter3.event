/*  Chapter 3
    Highest label: 7
    Temporary flags used:
      0x5 Raided fortress 1
      0x7 Raided fortress 2
      0x8 Raided fortress 3
      0x9 Raided fortress 4
      0xA Vivian & Linda talk. Also allows seize.
      0xB Vivian & Alvin BQ.
      0xC Alvin generic BQ.
      0xD := Flag 0xA | 0xB | 0xC. Vivian & Alvin talk.
      0xE Alvin & Linda BQ.
*/
{
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno Tutorial
POIN Traps1 Traps2
POIN PrepUnits PrepUnits  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

Dunno:
END_MAIN

Tutorial:
END_MAIN

PrepUnits:
UNIT

PlayerUnits1:
UNIT Demi Soldier_F Vivian Level(1, Ally, 0) [1,1] 0 0 0 0 [] NoAI
UNIT Theresa Archer_F Vivian Level(1, Ally, 0) [0,2] 0 0 0 0 [] NoAI
UNIT Herbert Cavalier Vivian Level(1, Ally, 0) [0,0] 0 0 0 0 [] NoAI
UNIT

PlayerUnits2L:
UNIT Vivian Supplier Vivian Level(1, Ally, 0) [5,0] 0 0 2 REDA1 [] NoAI
UNIT Leona Mercenary_F Vivian Level(1, Ally, 0) [6,0] 0 0 1 REDA2 [] NoAI
UNIT
PlayerUnits2M:
UNIT Vivian Supplier Vivian Level(1, Ally, 0) [5,0] 0 0 2 REDA1 [] NoAI
UNIT Milo Monk Vivian Level(1, Ally, 0) [6,0] 0 0 1 REDA2 [] NoAI
UNIT
  REDA1:
  REDA [3,0] 0 0x80 0 0
  REDA [3,1] 0 0x80 0 0
  REDA2:
  REDA [4,0] 0 0x80 0 0
  
SimonUnit:
UNIT Simon Shaman 0 Level(1, NPC, 0) [12,19] 0 0 0 0 [] NoAI
UNIT

EnemyUnits1:
UNIT Alvin Knight Alvin Level(11, Enemy, 0) [16,19] 0 0 0 0 [IronLance, Horseslayer, Concoction] [0x03,0x03,0x0,0x20]
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [13,20] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [18,17] 0 0 1 REDA3 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [18,17] 0 0 1 REDA4 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [18,17] 0 0 2 REDA5 [IronLance] AttackInRangeAI
UNIT Linda Cavalier Alvin Level(3, Enemy, 1) [16,19] 0 0 4 REDA6 [] AttackInRangeAI
UNIT
  REDA3:
  REDA [15,17] 0 0x80 0 0
  REDA4:REDA5:
  REDA [17,17] 0 0x80 0 0
  REDA [17,15] 0 0x80 0 0
  REDA6:
  REDA [16,20] 0 0x80 0 0
  REDA [9,20] 0 0x80 0 0
  REDA [9,16] 0 0x80 0 0
  REDA [8,16] 0 0x80 0 0

EnemyUnits2:
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [8,16] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [8,16] 0 0 2 REDA7 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [8,16] 0 0 2 REDA8 [IronLance] AttackInRangeAI
UNIT 0x4E PegasusKnight Alvin Level(3, Enemy, 1) [8,16] 0 0 2 REDA9 [IronLance] AttackInRangeAI
UNIT Linda Cavalier Alvin Level(3, Enemy, 1) [8,16] 0 0 1 REDA10 [] AttackInRangeAI
UNIT
  REDA7:
  REDA [8,17] 0 0x80 0 0
  REDA [11,17] 0 0x80 0 0
  REDA8:
  REDA [9,16] 0 0x80 0 0
  REDA [9,19] 0 0x80 0 0
  REDA9:
  REDA [8,20] 0 0x80 0 0
  REDA [3,20] 0 0x80 0 0
  REDA10:
  REDA [8,9] 0 0x80 0 0

LindaUnit1:
UNIT Linda Shaman_F Alvin Level(1, Enemy, 0) [16,19] 0 0 1 REDA6 [] NoAI
UNIT

EnemyUnits3:    // Loadbuffer overlaps with debufftable. Hence we split these in two.
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [1,18] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [3,11] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [0,8] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [3,4] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [6,0] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [13,5] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [10,8] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [18,9] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [17,10] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [16,6] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT
EnemyUnits4:
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [13,3] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [11,1] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [9,6] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [10,13] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [11,13] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [16,19] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [0,13] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [4,14] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [14,11] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT 0x4E Soldier_F Alvin Level(3, Enemy, 1) [8,4] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT

LindaUnit2:
UNIT Linda Shaman_F Alvin Level(9, Enemy, 0) [8,9] 0 0 2 REDA11 [Flux, Luna, Vulnerary] NeverMoveAI
UNIT
  REDA11:
  REDA [6,9] 0 0x80 0 0
  REDA [6,8] 0 0x80 0 0

LindaReinfUnits:
UNIT Carmen Cleric Vivian Level(11, Ally, 0) [6,8] 0 0 2 REDA12 [Heal, Mend, Concoction] NoAI
UNIT Vincent WyvernKnight Vivian Level(10, Ally, 0) [6,8] 0 0 2 REDA13 [IronAxe, Hatchet, Vulnerary] NoAI
UNIT
  REDA12:
  REDA [6,9] 0 0x80 0 0
  REDA [5,9] 0 0x80 0 0
  REDA13:
  REDA [7,8] 0 0x80 0 0
  REDA [7,9] 0 0x80 0 0

BeginningScene:
LOAD2 0 PlayerUnits1
ENUN
LOAD1 1 SimonUnit
ENUN
REMOVEPORTRAITS
BACG 0x35
FADU 128
FAWI 4
BACG 0x14
_0x1328 0x2C                // Fades in song apparently. Taken from FE8 Ch14.
FAWU 4
BROWNBOXTEXT 0x210 [8,8]
ShowText(Ch3_0)
_0x1328 0x7FFF
FAWI 4
BACG 0x12
REMA
FAWU 4
ShowText(Ch3_0)
REMA
FADI 16
CLEAN
FADU 16

CHECK_EVENTID MiloFlag
BNE 0 0x0 0xC
  LOAD2 0 PlayerUnits2L
  GOTO 1
LABEL 0
  LOAD2 0 PlayerUnits2M
LABEL 1
ENUN
CUMO Demi
STAL 60
CURE
FADI 16
Text(Plain_3,Ch3_0)

CAMERA [18,20]
MOVE 0x80 Simon [16, 19]
ENUN
DISA Simon
CUMO [16, 19]
STAL 60
CURE
FADI 16
Text(Plain_3,Ch3_0)

LOAD1 1 EnemyUnits1
ENUN
DISA Linda
STAL 20
LOAD1 1 EnemyUnits2
ENUN
DISA Linda
LOAD1 1 LindaUnit1
ENUN
CUMO Linda
STAL 60
CURE
FADI 16
LOAD1 1 EnemyUnits3
ENUN
LOAD1 1 EnemyUnits4
ENUN
Text(Plain_3,Ch3_0)

EVBIT_T 0x9
MOVE 0x80 Linda [8, 9]
ENUN
EVBIT_F 0x9
DISA Linda
CUMO [8, 9]
STAL 60
CURE
FADI 16
Text(Plain_3,Ch3_0)

LOAD1 1 LindaUnit2
ENUN
CAMERA [0,0]
CHECK_EVENTID MiloFlag
BNE 2 0x0 0xC
  RemoveRetreatedUnit(Leona)
  GOTO 3
LABEL 2
  RemoveRetreatedUnit(Milo)
LABEL 3
RemoveRetreatedUnit(Demi)
NoFade
ENDA

LocationBasedEvents:
Raid(0x5, Raid1, 14, 6)
Raid(0x7, Raid2, 8, 9)
Raid(0x8, Raid3, 8, 16)
Raid(0x9, Raid4, 0, 20)
Seize(16, 19)
END_MAIN
  Raid1:
  TileChange(14, 6)
  ENDA
  Raid2:
  TileChange(8, 9)
  ENDA
  Raid3:
  TileChange(8, 16)
  ENDA
  Raid4:
  TileChange(0, 20)
  ENDA

EndingScene:
ASMC ChapterEndExtra+0x1

CHECK_EVENTID 0xA
BNE 4 0x0 0xC
  // Linda joined already.
  GOTO 5
LABEL 4
  // Linda didn't join yet.
LABEL 5

MNCH 0x4
ENDA

TurnBasedEvents:
END_MAIN


/*
Flag D := A | B | C

if Alvin has fought any unit but Vivian:
  Set flag C, D in generic battle quote.
if Vivian talks to Linda:
  Set flag A, D.
if Vivian attacks Alvin and flag A or C is set, B unset:
  Battle conversation, set flag B, D.
if Vivian attacks Alvin and flag A & B are unset:
  Different battle conversation, sets flag B, D.
if Vivian talks to Alvin, only possible when Flag A, B, C (so D) unset:
  Conversation where Alvin tricks Vivian. Vivian gets killed.
*/
CharacterBasedEvents:
CharacterEvent(0xA,VivianLindaTalk,Vivian,Linda)
CharacterEvent(0xD,VivianAlvinTalk,Vivian,Alvin)
END_MAIN
  VivianLindaTalk:
  Text(Ch3_0)
  CUSA Linda
  SetChapterObjectiveText(0x1A0, 0x1B7) // "Seize gate", "Seize lair gate"
  LOAD1 1 LindaReinfUnits
  ENUN
  CUMO Vincent
  STAL 60
  CURE
  Text(Ch3_0)
  ENUT 0xD
  NoFade
  ENDA
  
  VivianAlvinTalk:
  Text(Ch3_0)
  ENUT 0x65     // GameOver.
  NoFade
  ENDA

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Traps1:
END_MAIN

Traps2:
END_MAIN
}

AlvinBQEvent:
CHECK_EVENTID 0xA
BNE 6 0x0 0xC
  CHECK_EVENTID 0xC
  BNE 6 0x0 0xC
    TEXTSHOW Ch3_BQ3
    GOTO 7
LABEL 6
  TEXTSHOW Ch3_BQ4
LABEL 7
TEXTEND
REMA
ENUT 0xD
ENDA
